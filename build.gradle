buildscript {
    repositories {
        if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
            maven { url "https://dl.bintray.com/alphagov/maven-test" }
            jcenter()
        } else {
            maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos/' }
        }
    }
    dependencies {
        classpath 'uk.gov.ida:ida-gradle:1.1.0-26',
                  'com.github.ben-manes:gradle-versions-plugin:0.21.0'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}
apply plugin: 'java'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'application'
apply plugin: 'maven-publish'


mainClassName = 'uk.gov.ida.eidas.trustanchor.cli.Application'
version = "1.0-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

repositories {
    if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
        logger.warn('Production builds MUST NOT be built with public binaries.\nUse artifactory/whitelisted-repos for production builds.\n\n')
        maven { url  'https://dl.bintray.com/alphagov/maven-test' }
        maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases' }
        jcenter()
    }
    else {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos/' }
    }
    mavenLocal()
}

dependencies {
    compile 'info.picocli:picocli:3.9.2',
            'org.json:json:20180813',
            "uk.gov.ida:saml-lib:3.4.2-205"

    testCompile 'uk.gov.ida:ida-dev-pki:1.1.0-37',
            'org.junit.jupiter:junit-jupiter-api:5.0.0',
            'org.mockito:mockito-core:2.23.4',
            'org.assertj:assertj-core:3.9.1'

    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0'
}

test {
    useJUnitPlatform()

    afterSuite { desc, result ->
        if (!desc.parent) {
            println "Results: ${result.resultType} " +
                    "(${result.testCount} tests, " +
                    "${result.successfulTestCount} successes, " +
                    "${result.failedTestCount} failures, " +
                    "${result.skippedTestCount} skipped)"
        }
    }
}

publishing {
    repositories {
        maven {
            credentials {
                username "${System.env.ARTIUSER}"
                password "${System.env.ARTIPASSWORD}"
            }
            url "https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/libs-release-local"
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = "uk.gov.ida.eidas"
            artifactId = "trust-anchor-cli"
            artifact shadowJar
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven-test'
        name = 'verify-eidas-trust-anchor-cli'
        userOrg = 'alphagov'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alphagov/verify-eidas-trust-anchor.git'
        version {
            name = version
        }
    }
}

